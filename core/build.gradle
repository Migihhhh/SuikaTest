import com.badlogic.gdx.tools.texturepacker.TexturePacker

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "com.badlogicgames.gdx:gdx-tools:1.12.1"
  }
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
eclipse.project.name = appName + '-core'

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]

dependencies {
  api "com.badlogicgames.ashley:ashley:$ashleyVersion"
  api "com.badlogicgames.box2dlights:box2dlights:$box2dlightsVersion"
  api "com.badlogicgames.gdx-controllers:gdx-controllers-core:$gdxControllersVersion"
  api "com.badlogicgames.gdx:gdx-ai:$aiVersion"
  api "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
  api "com.badlogicgames.gdx:gdx:$gdxVersion"
  api "com.github.tommyettinger:textratypist:0.10.0"
}

task packTextures {
  def inputDir = "$rootDir/assets/images/included"
  def outputDir = "$rootDir/assets/images/included/packed"
  inputs.dir inputDir
  outputs.dir outputDir
  doLast {
    delete(outputDir)
    TexturePacker.process(inputDir, outputDir, "images.pack")
  }
}

project.tasks.processResources.dependsOn packTextures
